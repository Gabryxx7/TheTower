 
class ViveController
{
	var mViveController;
	var mBoundingBoxControllerObj;
	
	var mControllerIndex;

	// Shader to draw the silhouettes of the objects contained in the ViveController
	var mSilhouetteShader;
	
	var mIsActionAvailable;
	
	var mTipLocalCoordinates;
	
	var mBoundingBox;
	
	var mWidth;
	var mHeight;
	var mDepth;
	
	var mIsVisible;
	
	var mInvisibleShader;
	var mBorderShader;
	
	var controllerMesh;
	var buttonMesh;
	var ledMesh;
	var leftGripMesh;
	var rightGripMesh;
	var systemButtonMesh;
	var trackpadMesh;
	var triggerMesh;
	
	var bodyObj;
	var buttonObj;
	var ledObj;
	var leftGripObj;
	var rightGripObj;
	var systemButtonObj;
	var trackpadObj;
	var triggerObj;
	
	var trackpadPointPos;
	var trackpadPointMesh;
	var trackpadPointObj;
	
	var buttonDepth;
	var trackpadDepth;
	var gripDepth;
	var leftGripPos;
	var rightGripPos;
	var systemButtonPos;
	var trackpadPos;
	var trackpadCenterOffset;
	var buttonPos;
	var triggerRange;
	var trackpadPointRange;
	
	var timeSinceLastTouch;
	var trackPointTime;
	var trackpointAlpha;
	
	// Public functions
	Draw();
	GetObj();
	GetBBoxObj();
	GetIndex();
	SetActionAvailable(isActionAvailable);
	GetControllerTipCoordinates();
	GetDirection();
	SetPosition(position);
	GetPosition();
	SetRotationMatrix(matrix);
	GetRotationMatrix();
	Rotate(angle, x, y, z);
	SetRotation(angle, x, y, z);
	LocalToWorld(point);
	Hide();
	Unhide();
	SetScale(x, y, z);
	IsVisible();
	GetBoundingBox();
	GetWidth();
	GetHeight();
	GetDepth();
	IsCollidingBBox(object);
	UpdateStatus(touchPadCoord, triggerCoord, isMenuPressed, isTriggerPressed, isGripPressed, isTouchpadPressed);
};


function ViveController::ViveController(controllerIndex)
{
	mControllerIndex = controllerIndex;
	
	controllerMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Body/body.aam");
	buttonMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Button/button.aam");
	ledMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Led/led.aam");
	leftGripMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Left_grip/l_grip.aam");
	rightGripMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Right_Grip/r_grip.aam");
	systemButtonMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/System_Button/sys_button.aam");
	trackpadMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Trackpad/trackpad.aam");
	triggerMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Trigger/trigger.aam");

	var fullControllerMesh = CVmNewMesh("Vive_Controller_Mesh/Exported/Full_Body/vr_controller_vive_1_5.aam");
	
	mInvisibleShader = CVmShaderProgram("void main(void) { gl_Position = ftransform(); }", "void main (void) { discard; }", true);
	
	mBoundingBoxControllerObj = CVmObj(fullControllerMesh);
	
	bodyObj = CVmObj(controllerMesh);
	buttonObj = CVmObj(buttonMesh);
	ledObj = CVmObj(ledMesh);
	leftGripObj = CVmObj(leftGripMesh);
	leftGripObj.SetPivotPoint(0, 0.2*SCALE_FACTOR, 0.375*SCALE_FACTOR);
	
	rightGripObj = CVmObj(rightGripMesh);
	rightGripObj.SetPivotPoint(0, 0.2*SCALE_FACTOR, 0.375*SCALE_FACTOR);
	
	systemButtonObj = CVmObj(systemButtonMesh);
	trackpadObj = CVmObj(trackpadMesh);
	triggerObj = CVmObj(triggerMesh);
	triggerObj.SetPivotPoint(0, -0.12*SCALE_FACTOR, 0.22*SCALE_FACTOR);
	
	buttonPos = buttonObj.GetPosition();
	leftGripPos = leftGripObj.GetPosition();
	rightGripPos = rightGripObj.GetPosition();
	systemButtonPos = systemButtonObj.GetPosition();
	trackpadPos = trackpadObj.GetPosition();
	trackpadCenterOffset = [0.0045*SCALE_FACTOR, 0.015*SCALE_FACTOR, 0.245*SCALE_FACTOR];
	triggerRange = -20;
	trackpadPointRange = [0.098*SCALE_FACTOR, -0.098*SCALE_FACTOR]; //for the x: -1 is left and 1 is right; for the y: -1 is bottom and 1 is top
	
	buttonDepth = 0.0075*SCALE_FACTOR;
	trackpadDepth = 4.5;
	gripDepth = 0.007*SCALE_FACTOR;
	
	trackPointTime = 400;
	trackpointAlpha = 1;
	trackpadPointMesh = CVmNewMesh(VRP_SPHERE, 60, 0.008*SCALE_FACTOR, 1.2);
	trackpadPointObj = CVmObj(trackpadPointMesh);
	trackpadPointPos = trackpadPos + trackpadCenterOffset;
	trackpadObj.AddChild(trackpadPointObj);
	trackpadObj.SetPivotPoint(trackpadCenterOffset);
	
	mViveController = CVmObj();
	mViveController.AddChild(bodyObj);
	mViveController.AddChild(buttonObj);
	mViveController.AddChild(ledObj);
	mViveController.AddChild(leftGripObj);
	mViveController.AddChild(rightGripObj);
	mViveController.AddChild(systemButtonObj);
	mViveController.AddChild(trackpadObj);
	mViveController.AddChild(triggerObj);
	
	mBoundingBox = fullControllerMesh.GetBoundingBox();
	var point1 = [mBoundingBox[0], mBoundingBox[1], mBoundingBox[2]];
	var point2 = [mBoundingBox[3], mBoundingBox[4], mBoundingBox[5]];
	
	mWidth = abs(point2.x - point1.x);
	mHeight = abs(point2.y - point1.y);
	mDepth = abs(point2.z - point1.z);
	
	mTipLocalCoordinates = [0, -0.38 * SCALE_FACTOR, -0.20 * SCALE_FACTOR];
	
	mSilhouetteShader = CVmShaderProgram("silhouetteShader.glsl");
	
	mIsActionAvailable = false;
	
	mIsVisible = true;
	
	mBorderShader = CVmShaderProgram("void main(void) { gl_Position = ftransform(); }", "void main (void) { gl_FragColor = vec4(0.8, 0.8, 0.0, 1.0); }", true);
	
	timeSinceLastTouch = 0;
}

/*
 * This method updates the visual status of the vive controller
 */
function ViveController::UpdateStatus(touchPadCoord, triggerCoord, isMenuPressed, isTriggerPressed, isGripPressed, isTouchpadPressed){
	if(touchPadCoord[0] == 0 && touchPadCoord[1] == 0) {
		var elapsedTime = GetTime() - timeSinceLastTouch; 
		if(elapsedTime > trackPointTime){
			trackpointAlpha = 0;	
		}
		else{			
			trackpointAlpha -= 0.025; //1/400 = 0.025, I don't know why but the floats are not working, I cannot use like trackPointTime/elapsedTime, no idea why
			outputln(trackpointAlpha);
		}
	}
	else{
		timeSinceLastTouch = GetTime();
		trackpointAlpha = 1;
		var yOffsetFactor = (touchPadCoord[1] > 0 ? touchPadCoord[1]: touchPadCoord[1]*0.4);
		yOffsetFactor += abs(touchPadCoord[0])*0.3;
		trackpadPointPos = trackpadPos + [trackpadCenterOffset.x, trackpadCenterOffset.y*yOffsetFactor+0.026*SCALE_FACTOR, trackpadCenterOffset.z] + [touchPadCoord[0] * trackpadPointRange[0], 0, touchPadCoord[1] * trackpadPointRange[1]];	
		trackpadPointObj.SetPosition(trackpadPointPos);
	}
	trackpadPointMesh.ModulateMaterials(1,1,1,trackpointAlpha);

	if(triggerCoord[0] > 0){
		if(isTriggerPressed < 0){
			triggerObj.SetRotation(triggerRange * triggerCoord[0], 1, 0, 0);
			triggerObj.SetPosition(0,0, 0.009 * triggerCoord[0]*SCALE_FACTOR);
		}else{
			triggerObj.SetPosition(0,0,0.012*SCALE_FACTOR);
			triggerObj.SetRotation(triggerRange-9, 1, 0, 0);			
		}
	}
	else
		triggerObj.SetRotation(0, 1, 0, 0);
	
	if(isMenuPressed > 0){
		buttonObj.SetPosition(buttonPos - [0, buttonDepth, 0]);
	}
	else{
		buttonObj.SetPosition(buttonPos);		
	}
	
	if(isGripPressed > 0){
//		leftGripObj.SetPosition(leftGripPos + [gripDepth, 0, 0]);
//		rightGripObj.SetPosition(rightGripPos - [gripDepth, 0, 0]);
		leftGripObj.SetRotation(2.5,0,1,0);
		rightGripObj.SetRotation(-2.5,0,1,0);
	}
	else{
//		leftGripObj.SetPosition(leftGripPos);
//		rightGripObj.SetPosition(rightGripPos);	
		leftGripObj.SetRotation(0,0,1,0);
		rightGripObj.SetRotation(0,0,1,0);
	}
	
	if(isTouchpadPressed > 0){
//		trackpadObj.SetPosition(trackpadPos - [0, 0.012*SCALE_FACTOR, 0]);
		trackpadObj.SetRotation(-trackpadDepth*touchPadCoord[1], 1, 0, 0);
		trackpadObj.Rotate(-trackpadDepth*touchPadCoord[0], 0, 0, 1);
	}
	else{
//		trackpadObj.SetPosition(trackpadPos);	
		trackpadObj.SetRotation(0,1,0,0);		
		trackpadObj.SetRotation(0,0,0,1);	
	}
}

/*
 * This method draws the ViveController and any objects that was added to it. It makes sure to draw each object's silhouette and on top of it
 * the object itself using any shader that was added to it
 */
function ViveController::Draw()
{
	if(mIsActionAvailable)
	{
    	glCullFace (GL_FRONT);
    	
		mViveController.SetScale(1.03 , 1.03, 1.03);
	
		mBorderShader.Start();		
			mViveController.Draw();
		mBorderShader.Stop();
	
		// Now I draw the front faces
		glCullFace (GL_BACK);
		
		// Reactivate the shader and reset the scale
		mViveController.SetScale(1.0, 1.0, 1.0);
	}
	
	mInvisibleShader.Start();	
		mBoundingBoxControllerObj.Draw();
	mInvisibleShader.Stop();
	
	mViveController.Draw(VR_NO_CULLING);
}

function ViveController::GetObj()
{
	return mViveController;
}

function ViveController::GetBBoxObj()
{
	return mBoundingBoxControllerObj;
}

function ViveController::GetIndex()
{
	return mControllerIndex;
}

function ViveController::SetActionAvailable(isActionAvailable)
{
	mIsActionAvailable = isActionAvailable;
}

function ViveController::GetControllerTipCoordinates()
{
	return mViveController.LocalToWorld(mTipLocalCoordinates);
}

function ViveController::GetDirection()
{
	var center = mViveController.LocalToWorld([0, 0, 0]);
	var tip = mViveController.LocalToWorld([0, 0, -1]);
	
	return (tip - center);
}

function ViveController::SetPosition(position)
{
	mViveController.SetPosition(position);
	mBoundingBoxControllerObj.SetPosition(position);
}

function ViveController::GetPosition()
{
	return mViveController.GetPosition();
}

function ViveController::SetRotationMatrix(matrix)
{
	mViveController.SetRotationMatrix(matrix);
	mBoundingBoxControllerObj.SetRotationMatrix(matrix);
}

function ViveController::GetRotationMatrix()
{
	return mViveController.GetRotationMatrix();
}


function ViveController::Rotate(angle, x, y, z)
{
	mViveController.RotateAbs(angle, x, y, z);
	mBoundingBoxControllerObj.RotateAbs(angle, x, y, z);
}

function ViveController::SetRotation(angle, x, y, z)
{
	mViveController.SetRotation(angle, x, y, z);
	mBoundingBoxControllerObj.SetRotation(angle, x, y, z);
}

function ViveController::LocalToWorld(point)
{
	return mViveController.LocalToWorld(point);
}

function ViveController::Hide()
{
	mViveController.Hide();
	mIsVisible = false;
}

function ViveController::Unhide()
{
	mViveController.Unhide();
	mIsVisible = true;
}

function ViveController::SetScale(x, y, z)
{
	mViveController.SetScale(x, y, z);
	mBoundingBoxControllerObj.SetScale(x, y, z);
}

function ViveController::IsVisible()
{
	return mIsVisible;
}

function ViveController::GetBoundingBox()
{
	return mBoundingBox;
}

function ViveController::GetWidth()
{
	return mWidth;
}

function ViveController::GetHeight()
{
	return mHeight;
}

function ViveController::GetDepth()
{
	return mDepth;
}

function ViveController::IsCollidingBBox(object)
{
	return mBoundingBoxControllerObj.IsCollidingBBox(object);
}
