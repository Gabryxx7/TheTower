class LevelMenuSelector{
	var mAnimator;
	var mObj;
	var mTextObj;
	var mTextColor;
	var mColor;
	var mSelectedColor;
	var mPos;
	var mFont;
	var mLevelNumber;
	var mTxtSize;
	var mWidth;
	var mHeight;
	var mThickness;
	var mIsSelected;
	
	var mRelatedImage;
	
	var mLongSideMesh;
	var mShortSideMesh;
	var mBackgroundMesh;
	
	Draw();
	SetPosition(pos);
	SetRotation(deg, x,y,z);
	GetObj();
	GetWidth();
	GetHeight();
	GetImage();
	IsSelected(selection);
	IsBeingPointedBy(mObject, objectPosition, distance);
};

function LevelMenuSelector::LevelMenuSelector(animator, levelNumber, relatedImage, pos, length, height, thickness, textColor, color, selectedColor, txtSize, font){
	mObj = CVmObj();
	mIsSelected = false;	
	mRelatedImage = relatedImage;
	mAnimator = animator;
	mLevelNumber = levelNumber;
	mTextColor = valtype(textColor) != "" ? textColor : [1,1,1];
	mColor = valtype(color) != "" ? color : [1,1,1];
	mSelectedColor = valtype(selectedColor) != "" ? selectedColor : [1,1,1];
	mPos = valtype(pos) != "" ? pos : [0,0,0];
	mFont = valtype(font) != "" ? font : "DS-Digital";
	mTxtSize = valtype(txtSize) != "" ? txtSize : 0.25;
	mWidth = valtype(length) != "" ? length : 6*SCALE_FACTOR;
	mHeight = valtype(height) != "" ? height : 3*SCALE_FACTOR;
	mThickness = valtype(thickness) != "" ? thickness : 0.5*SCALE_FACTOR;
	
	var ambient = [1,1,1];
	var diffuse = mColor;
	var specular = [0.2, 0.2, 0.2];
	var shininess = 0;
	var lightMin = 0;
	var lightMax = 2;
	var alpha = 1;
	mLongSideMesh = CVmNewMesh(VRP_PLANE, 1, [-mWidth/2, -mThickness/2, 0], [mWidth/2, mThickness/2, 0], 2,2 );
	AddToonShaderToMesh(mLongSideMesh, ambient, diffuse, specular, shininess, lightMin, lightMax, SCALE_FACTOR, alpha);
	mShortSideMesh = CVmNewMesh(VRP_PLANE, 1, [-mThickness/2, -mHeight/2, 0], [mThickness/2, mHeight/2, 0], 2,2 );
	AddToonShaderToMesh(mShortSideMesh, ambient, diffuse, specular, shininess, lightMin, lightMax, SCALE_FACTOR, alpha);
	mBackgroundMesh = CVmNewMesh(VRP_PLANE, 1, [-mWidth/2, -mHeight/2, 0], [mWidth/2, mHeight/2, 0], 2,2 );
	AddToonShaderToMesh(mBackgroundMesh, ambient, diffuse, specular, shininess, lightMin, lightMax, SCALE_FACTOR, 0.5);
	
	var levelString = "Level " + char(levelNumber+48);
	var txt = CVmText(levelString,0,0,mFont, 0);		
	txt.SetScale(mTxtSize);
	txt.SetColor(mTextColor.r, mTextColor.g, mTextColor.b);		
	
	mTextObj = CVmObj();
	mTextObj.LinkToText(txt);
	
	var mTextOffset = 0.05 *SCALE_FACTOR;
	mObj.AddChild(CvmObj(mBackgroundMesh), 0, 0, 0);
	mObj.AddChild(CvmObj(mLongSideMesh), 0, mHeight/2+mThickness/2, 0);
	mObj.AddChild(CvmObj(mLongSideMesh), 0, -mHeight/2-mThickness/2, 0);
	mObj.AddChild(CvmObj(mShortSideMesh), -mWidth/2+mThickness/2, 0, 0);
	mObj.AddChild(CvmObj(mShortSideMesh), mWidth/2-mThickness/2, 0, 0);
	mObj.AddChild(mTextObj, -(len(levelString)*(mTxtSize + 0.025))*SCALE_FACTOR, -mTxtSize*SCALE_FACTOR, mTextOffset);
	mObj.SetPosition(mPos);
}


function LevelMenuSelector::Draw(){
	mObj.Draw();
}	

function LevelMenuSelector::SetPosition(pos){
	mObj.SetPosition(pos);
}

function LevelMenuSelector::SetRotation(deg, x,y,z){
	if(valtype(x) == "V")
		mObj.SetRotation(deg, x);
	else
		mObj.SetRotation(deg, x,y,z);
}

function LevelMenuSelector::GetHeight(){
	return mHeight+mThickness*2;
}

function LevelMenuSelector::GetWidth(){
	return mWidth;
}

function LevelMenuSelector::GetObj(){
	return mObj;
}

function LevelMenuSelector::GetImage(){
	return mRelatedImage;
}

function LevelMenuSelector::IsBeingPointedBy(mObject, objectPosition, distance){
	if(mIsSelected)
		return false;
		
	var raycastOrigin = objectPosition;
	var raycastEnd = raycastOrigin + mObject.GetDirection() * distance;
	var contactPoint = IsObjectSelected(raycastOrigin, raycastEnd, mObj);
	
	if(contactPoint != Void){
		mBackgroundMesh.ShaderSetParameter(0, "diffuse", 0, mSelectedColor);
		mLongSideMesh.ShaderSetParameter(0, "diffuse", 0, mSelectedColor);
		mShortSideMesh.ShaderSetParameter(0, "diffuse", 0, mSelectedColor);
		return true;		
	}
	
	mBackgroundMesh.ShaderSetParameter(0, "diffuse", 0, mColor);
	mLongSideMesh.ShaderSetParameter(0, "diffuse", 0, mColor);
	mShortSideMesh.ShaderSetParameter(0, "diffuse", 0, mColor);
		
	return false;	
}

function LevelMenuSelector::IsSelected(selection){
	if(valtype(selection) != "B")	
		return mIsSelected;
	
	if(selection){
		mIsSelected = true;
		mBackgroundMesh.ShaderSetParameter(0, "diffuse", 0, mSelectedColor);
		mLongSideMesh.ShaderSetParameter(0, "diffuse", 0, mSelectedColor);
		mShortSideMesh.ShaderSetParameter(0, "diffuse", 0, mSelectedColor);	
		return true;
	}
	else{
		mIsSelected = false;
		mBackgroundMesh.ShaderSetParameter(0, "diffuse", 0, mColor);
		mLongSideMesh.ShaderSetParameter(0, "diffuse", 0, mColor);
		mShortSideMesh.ShaderSetParameter(0, "diffuse", 0, mColor);
		return false;
	}
}